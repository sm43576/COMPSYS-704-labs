 import run.GUI;

Plant(
		
		
		// Global ////////////////////////////////////////////////////////
		input signal  enable,refill, removeBottle; //TODO: need remove bottle in GUI
		
		// Conveyor/////////////////////////////////////////////////////// 
		input signal motConveyorOnOff, motorPos1On, motorPos5On;
		output signal bottleAtPos1, bottleAtPos5, RTbottleAtPos1, bottleLeftPos5;

		
		output signal bottleAtPos1E, bottleAtPos5E, motConveyorOnOffE;
		///////////////////////////////////////////////////////////////////
		
		// Filler
		input signal valveInjectorOnOff,valveInletOnOff,dosUnitValveRetract,dosUnitValveExtend; //to plant
		input Integer signal selectCanister, selectPercentage;
		output signal dosUnitEvac, valveInjectorActive;  // from plant
		output Integer signal selectedCanister, selectedPercentage,dosUnitFilled; // from PLANT
		
		
		///////////////////////////////////////////////////////////////////
		
		// CapLoader
		
		input signal pusherExtend,vacOn,armSource,armDest,magReq;
		output signal pusherRetracted,pusherExtended,WPgripped,armAtSource,armAtDest,magEmpty;
		/////////////////////////////////////////////////////////////////////	
		
		
		// Capper
		
		//Input signals from capper controller
		input signal cylPos5ZAxisExtend; 
		input signal gripperTurnRetract, gripperTurnExtend;
		input signal capGripperPos5Extend;
		input signal cylClampBottleExtend;
		
		//Output signals to capper controller
		output signal bottleAtPos4;
		output signal gripperZAxisLowered;
		output signal gripperZAxisLifted;
		output signal gripperTurnHomePos;
		output signal gripperTurnFinalPos;
		output signal gripperGrippedCap;
		output signal cylClampedBottle;
		/////////////////////////////////////////////////////////////////////
		
		
		//Rotatry Table
		
		input signal rotaryTableTrigger;
		output signal tableAlignedWithSensor, capOnBottleAtPos1, bottleAtPos2, bottleAtPos3; //bottleAtPos1; dupe
		output signal moveToPos5, moveToPos4, moveToPos3, moveToPos2;
		
		
		/////////////////////////////////////////////////////////////////////
		
		
		
		
		)
->
{
//	new Thread(new GUI()).start();
	// Caploader internal signals
	signal capDec;
	int signal capPos;
	emit capPos(0);
	signal disablePos5;
////////////////////////////////////////////////////////////////////////////////////////////////
//Conveyor
	{
		// LOGIC HERE
		while(true){
			present(motConveyorOnOff){
				waitl(3 s);
				{
					present(motorPos1On){ // motor is on and move bottle at pos 1
						System.out.println("Conveyor plant - moving bottle to 1");
						abort(moveToPos2) {
							sustain bottleAtPos1;
						}
					}
				}
				||
				{
					present(motorPos5On){ // motor is on and remove bottle at pos 5
						System.out.println("Conveyor Plant - moving bottle to leave 5");
						abort(!motorPos5On && !motConveyorOnOff) {
							sustain bottleLeftPos5;
						}
					}
				}
				
			}
//			System.out.println("motor is off");
			pause;
		}
	}		
		
	||
	{
		// To tell rotary table that bottle is at position 1 since you can't have a signal going into two clock domains
		while(true) {
			present(bottleAtPos1){
//				System.out.println("sending info to rotary that bottle is at position1");
				emit RTbottleAtPos1; 
			}
//			System.out.println("no longer");
			pause;
		}
	}
	
	||
	
	//Filler /////////////////////////////////////////////////////////////////////
	{
		int signal canister;
		int signal percent;
		{ //Filler canister selection.
			while(true) {
				//Select canister
				await(selectCanister); 
				waitl(1 s);
				emit canister(#selectCanister); //Record selected canister
				emit selectedCanister(#selectCanister); //Send back ok.
				pause;
			}
		}
		||
		{ //Filler percentage selection.
			while(true) {
				//Select percentage.
				await(selectPercentage);
				emit percent(#selectPercentage); //Record selected percentage
				emit selectedPercentage(#selectPercentage); //Send back ok.
				pause;
			}
		}
		||
		{ //Filler Injector
			while(true) {
				present(valveInjectorOnOff){
					abort(!valveInjectorOnOff){
						sustain valveInjectorActive;
					}
				}
				pause;
			}
		}
		||
		{ //Pressure canister.
			while(true) {
				
				present(dosUnitValveExtend && !dosUnitValveRetract){
					abort (dosUnitValveRetract){
						sustain dosUnitEvac; 
					}
				}
				present(dosUnitValveRetract && !dosUnitValveExtend) {
					System.out.println("FillerPlant: Filling with canister " + #canister + " Percent: " + #percent);
					abort (dosUnitValveExtend) {
						sustain dosUnitFilled; 
					}
				}
				pause;
				
			}
		}

	}
	/////////////////////////////////////////////////////////////////

	||
	
//Cap Loader////////////////////////////////////////////	
	{// Arm
		while(true){
			present(armSource && !armDest){
				abort (armDest){
					sustain armAtSource; 
				}
			}
			present(armDest && !armSource) {
				abort (armSource) {
					sustain armAtDest;
				}
			}
			pause;
		}
	}
	||
	{// Pusher
		while(true){
			abort(pusherExtend){
				sustain pusherRetracted;
			}
			waitl(3 s); 
			//System.out.println("Aborted");
			abort(!pusherExtend){
				sustain pusherExtended;
			}
			pause;
		}
	}
	||
	{// Vacuum
		while(true){
			present(vacOn && armAtSource){
				abort(magEmpty || !vacOn) {
					sustain WPgripped;
				}
			}
			pause;
		}
	}
	||
	{
		while(true){
			abort(magEmpty){
				while(true){
						await(pusherRetracted);
						await(pusherExtended);
						await(pusherRetracted);
						emit capDec;
						pause;
				}
			}
			await(magReq);
			pause;
		}
	}
	||
	{
		int capcount = 5;
		while(true){
			//System.out.println("CAPCOUNT" + capcount);
			present(capDec){
				if(capcount > 0)
					capcount--;
			}
			present(magReq){
				capcount = 5;
			}
			if(capcount == 0) emit magEmpty;
			pause;
		}
	}
	||
	
	// Capper
		{
			while(true) {
				await(moveToPos4);//bottle is in position 4. Need to inform controller. 
				abort(moveToPos5) {
					sustain bottleAtPos4; //Maintain bottleAtPos4 signal until a signal is received to turn the table.
				}
				pause;
			}
		}
		||
	//Cap Screwer - Move Gripper
		{
			while(true) {
				abort(cylPos5ZAxisExtend) {
					System.out.println("Gripper Extended");
					sustain gripperZAxisLifted; //If cylPos5ZaxisExtend is absent, sustain lifted signal.
				}
				System.out.println("Gipper Tranistion");
				abort(!cylPos5ZAxisExtend) {
					System.out.println("gripper lowered");
					sustain gripperZAxisLowered; //If cylPos5ZaxisExtend is present, sustain lowered signal.
				}
				pause;
			}
		}
		||
	//Cap Screwer - Gripper
		{
			while(true) {
				await(capGripperPos5Extend);
				waitl(3 s);
				abort(!capGripperPos5Extend) {
					sustain gripperGrippedCap; 
				}
				pause;
			}
		}
		||
	//Cap Screwer - Twister
		{
			while(true) {
				present(gripperTurnExtend && !gripperTurnRetract){
					abort(gripperTurnRetract){
						sustain gripperTurnFinalPos; 
					}
				}
				present(gripperTurnRetract && !gripperTurnExtend) {
					abort(gripperTurnExtend) {
						sustain gripperTurnHomePos; 
					}
				}
				pause;
			}
		}
		|| 
	//Cap Screwer - Clamps
		{
			while(true) {
				await(cylClampBottleExtend);
				
				abort (!cylClampBottleExtend) {
					System.out.println("ClampBottle");
					sustain cylClampedBottle; //Maintain bottleAtPos4 signal until a signal is received to turn the table.
				}
				pause;
			}
		}
	||	
	//Rotary Table
	{
		while(true) {
			present(rotaryTableTrigger){
				
				// Turn the table 60 degrees and move the bottles along by one somehow
				//Ensure the table positions are aligned with the sensors via tableAlignedWithSensor.
				System.out.println("table triggered now moving");
				
				// if there is a bottle at pos 4 move to pos 5
				present(bottleAtPos4) {
					System.out.println("moving bottle at pos 4 to pos 5");
					emit moveToPos5;
				}
				
				// if there is a bottle at pos 3 move to pos 4
				present(bottleAtPos3){
					System.out.println("moving bottle at pos 3 to pos 4");
					emit moveToPos4;
				}
				// if there is a obttle at pos 2 move to pos 3
				
				present(bottleAtPos2){
					System.out.println("moving bottle at pos 2 to pos 3");
					emit moveToPos3;
				}
				// if there is a bottle at pos 1 move to pos 2.
				present(bottleAtPos1){
					System.out.println("moving bottle at pos 1 to pos 2");
					emit moveToPos2;
				}
				// emit table aligned with sensor
				System.out.println("Table has been aligned");
				
				abort(!rotaryTableTrigger) {
					sustain tableAlignedWithSensor;
				}				
			}
				
			pause;
				
		}
	}
	||
	{
		while(true) {
			abort(bottleLeftPos5){ // stop emitting bottleAtPos5 when bottle has left pos 5
				present(moveToPos5){
					System.out.println("bottle is now at pos 5"); 
					sustain bottleAtPos5;
				} 
			}
			pause;
		}
	}
	||
	{while(true) {present(moveToPos4){System.out.println("bottle is now at pos 4"); sustain bottleAtPos4;}pause;}}
	||
	{while(true) {present(moveToPos3){System.out.println("bottle is now at pos 3"); sustain bottleAtPos3;}pause;}}
	||
	{while(true) {present(moveToPos2){System.out.println("bottle is now at pos 2"); sustain bottleAtPos2;}pause;}}

}
